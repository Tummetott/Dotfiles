# vim: set ft=zsh:

# This line sources shell-independent environment variables. While there is
# considerable debate about whether such variables should be sourced in zprofile
# or zshrc, sourcing them in zshrc is preferred for broader compatibility across
# linux and macOS systems. This ensures that the environment variables are
# consistently available across all interactive shells, regardless of how
# they're initiated. Given the efficiency of modern computers, the performance
# impact of sourcing these variables for every new interactive shell is
# negligible.
[ -f ~/.config/shell/env ] && source ~/.config/shell/env

# Initialize Powerlevel10k instant prompt. Keep this block near the top of zshrc
# to ensure the prompt appears immediately when a new shell session starts.
if [[ -r "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh" \
    && ( -n "$TMUX" || -n "$SSH_TTY" ) ]]; then
  source "$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Hostname up to the first '.'
export SHORT_HOST=${SHORT_HOST:-${(%):-%m}}

# Define new compdump location. The zsh directory must be created manually
export ZSH_COMPDUMP="$XDG_CACHE_HOME/zsh/zcompdump-${SHORT_HOST}-${ZSH_VERSION}"

# When pressing ESC, use a minimal timeout of 1 ms
export KEYTIMEOUT=1

export HISTFILE="$XDG_STATE_HOME/zsh/history"
export ZSH_SESSIONS_DIR="$XDG_STATE_HOME/zsh/sessions"

# Prevent zgen from running compinit. This is already done by oh-my-zsh
export ZGEN_AUTOLOAD_COMPINIT=0

# Remove duplicates in paths
typeset -gU cdpath fpath mailpath path

# Show no duplicates in history
setopt HIST_FIND_NO_DUPS

# Oh-my-zsh settings. Must be defined before oh-my-zsh is loaded
HYPHEN_INSENSITIVE='true' # Treat '-' and '_' the same for completion purposes
DISABLE_AUTO_TITLE='true' # Disable auto-setting terminal title
COMPLETION_WAITING_DOTS='true' # Show red dots while waiting for completion

# Load custom shell functions. This must be loaded before oh-my-zsh calls
# compinit
fpath=( $XDG_CONFIG_HOME/shell/functions "${fpath[@]}" )
autoload -Uz $fpath[1]/*(.:t)

# Load custom completions. This must be loaded before oh-my-zsh calls
# compinit
# HINT: Delete compiled completions and recompile them with:
# 'rm -f $ZSH_COMPDUMP; compinit -d "$ZSH_COMPDUMP"'
fpath=( $XDG_CONFIG_HOME/zsh/completions "${fpath[@]}" )
autoload -Uz $fpath[1]/*(.:t)

# Update the highlight color of ZVM. Must be defined before ZVM is loaded
function zvm_config() {
    ZVM_VI_HIGHLIGHT_BACKGROUND=8
    ZVM_VI_HIGHLIGHT_FOREGROUND=0
}

# To ensure that FZF's keybindings take precedence, we need to source FZF after
# ZVM, as both have some default keybindings that overlap. Must be defined
# before ZVM is loaded
function zvm_after_init() {
    if [ -f "$XDG_CONFIG_HOME/fzf/fzf.zsh" ]; then
        source "$XDG_CONFIG_HOME/fzf/fzf.zsh"
        # FZF default mapping is CTRL-t. I want to keep the transpose-char feature and
        # remap it to CTRL-g
        bindkey '^G' fzf-file-widget
        bindkey '^T' transpose-chars
    fi
}

# Bootstrap plugin manager
if [[ ! -f "$ZGEN_DIR/zgen/zgen.zsh" ]]; then
    git clone 'https://github.com/tarjoilija/zgen.git' "$ZGEN_DIR/zgen"
fi

# Load plugin manager
source "$ZGEN_DIR/zgen/zgen.zsh"

# 'zgen' creates a static init script consisting of source statements for plugins
# and pmodload statements for prezto modules. When ZSH starts up, zgen will
# source the init script directly without parsing its dialect every time, which
# also means you need to regenerate the init script after updating your zshrc.
# Remove the init script with: 'zgen reset'
if ! zgen saved; then

    # Add additional completions for zsh. The completions must be loaded before
    # oh-my-zsh in order to be included into the 'compinit' call
    zgen load esc/conda-zsh-completion
    zgen load zsh-users/zsh-completions

    # Use the base components of oh-my-zsh. Load this framework at the very
    # beginning (after the completions).
    zgen oh-my-zsh

    # Load the awesome prompt theme 'powerlevel10k'.
    zgen load romkatv/powerlevel10k powerlevel10k

    # Zgen should only source the 'profile_helper.sh' script
    zgen load tinted-theming/base16-shell profile_helper.sh main

    # As you type commands, you will see a completion offered after the cursor
    # in a muted gray color
    zgen load zsh-users/zsh-autosuggestions

    # This plugin provides syntax highlighting for the shell zsh. It enables
    # highlighting of commands whilst they are typed.
    zgen load zsh-users/zsh-syntax-highlighting

    # This plugin provides the real vim experience in zsh. It offers mode
    # sensitive cursor styling and all vim keybindings
    zgen load jeffreytse/zsh-vi-mode

    # Fuzzy finder fzf, installed from a git repository instead of package
    # manager. This approach provides a platform-independent installation method
    # that is self-contained within my configuration setup.
    zgen load junegunn/fzf

    # The installation scrip of fzf downloads the executable and generates a
    # script in $XDG_CONFIG_HOME/fzf/fzf.zsh for keybindings and completion.
    if [[ ! -x "$FZF_BASE/bin/fzf" ]]; then
        "$FZF_BASE/install" --key-bindings --completion --no-update-rc --xdg
    fi

    # Add BASE16 color schemes for 'fzf'
    zgen load tinted-theming/base16-fzf . main

    # generate the init script from plugins above
    zgen save
fi

# oh-my-zsh enables shared-history. I don't want that
unsetopt share_history

# zsh wants you to verify history expansion. I want to type 'sudo !!' without
# further verification
unsetopt HIST_VERIFY

# Source the 'powerlevel10k' config
[ -f ${ZDOTDIR}/p10k.zsh ] && source ${ZDOTDIR}/p10k.zsh

# With 'base16-shell', the default 'zsh-autosuggestions' color (color8) closely
# matches the terminal foreground color, making it hard to distinguish
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=19'

# Scroll history with CTRL-n and CTRL-p
bindkey '^p' up-line-or-search
bindkey '^n' down-line-or-search

# Because of VIM-TMUX Navigator, I can't kill lines with CTRL-k. I remap this
# to CTRL-x
bindkey '^X' kill-line

# Check if the programm silver searcher 'fd' exists
if (( $+commands[fd] )); then
    # Use fd instead of the default find command for listing path & directory
    # candidates (e.g. for cd **) We could use ripgrep here. However, it does
    # not support finding directories.
    _fzf_compgen_path() {
        fd --hidden --follow --exclude ".git" --exclude . "$1"
    }
    _fzf_compgen_dir() {
        fd --type d --hidden --follow --exclude ".git" . "$1"
    }
fi

# The plugin 'syntax-highlighting' adds undercores to paths. Let's turn that off
(( ${+ZSH_HIGHLIGHT_STYLES}  )) || typeset -A ZSH_HIGHLIGHT_STYLES
ZSH_HIGHLIGHT_STYLES[path]=none
ZSH_HIGHLIGHT_STYLES[path_prefix]=none
ZSH_HIGHLIGHT_STYLES[autodirectory]=none
ZSH_HIGHLIGHT_STYLES[precommand]=fg=green

# Source lf icons file
# [ -f ~/.config/lf/lficons ] && source ~/.config/lf/lficons

# Bind my custom 'lfcd' function to ctrl-o
# bindkey -s '^o' 'lfcd\n'

# Source my aliases file
[ -f "$XDG_CONFIG_HOME/shell/aliases" ] && source "$XDG_CONFIG_HOME/shell/aliases"

# Initialize conda
if [ -f "$CONDA_HOME/etc/profile.d/conda.sh" ]; then
    source "$CONDA_HOME/etc/profile.d/conda.sh"
fi

# Autostart tmux
if (( $+commands[tmux] )); then
    autostart-tmux
fi

# Return with exit code 0
true
